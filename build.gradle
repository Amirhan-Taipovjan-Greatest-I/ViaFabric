import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'com.palantir.git-version' version '0.12.0-rc2'
}

group 'com.github.creeper123123321.viafabric'
version '0.1.0-SNAPSHOT+' + gitVersion()
archivesBaseName = 'ViaFabric'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://repo.viaversion.com/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://maven.fabricmc.net/' }
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}

dependencies {
    // transitive = false, viabackwards-core because Guava is conflicting on runClient
    compile("us.myles:viaversion:2.0.0-19w09a") { transitive = false }
    compile("de.gerrygames:viarewind-core:1.4.0") { transitive = false }
    compile("nl.matsv:viabackwards-core:2.4.0-SNAPSHOT") { transitive = false }

    minecraft "com.mojang:minecraft:19w09a"
    mappings "net.fabricmc:yarn:19w09a.2"
    modCompile "net.fabricmc:fabric-loader:0.3.7.109"

    modCompile "net.fabricmc:fabric:0.2.3.108"
}

minecraft {
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}