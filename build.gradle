import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'com.palantir.git-version' version '0.12.0-rc2'
}

group 'com.github.creeper123123321.viafabric'
version '0.1.0-SNAPSHOT+' + gitVersion()
archivesBaseName = 'ViaFabric'
description = 'Client-side and server-side ViaVersion for Fabric'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://repo.viaversion.com/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://maven.fabricmc.net/' }
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property('version'),
            "description": project.property('description')
    ]
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    // transitive = false, viabackwards-core because Guava is conflicting on runClient
    shade("us.myles:viaversion:2.0.0-19w11b") { transitive = false }
    shade("de.gerrygames:viarewind-core:1.4.0") { transitive = false }
    shade("nl.matsv:viabackwards-core:3.0.0-19w11b") { transitive = false }
    
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    minecraft "com.mojang:minecraft:19w11b"
    mappings "net.fabricmc:yarn:19w11b.4"
    modCompile "net.fabricmc:fabric-loader:0.3.7.109"

    modCompile "net.fabricmc:fabric:0.2.3.111"
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            //exclude 'META-INF', 'META-INF/**'
            exclude 'us/myles/ViaVersion/BungeePlugin.class'
            exclude 'us/myles/ViaVersion/SpongePlugin.class'
            exclude 'us/myles/ViaVersion/VelocityPlugin.class'
            exclude 'us/myles/ViaVersion/ViaVersionPlugin.class'
            exclude 'us/myles/ViaVersion/bukkit/**'
            exclude 'us/myles/ViaVersion/velocity/**'
            exclude 'us/viaversion/libs/javassist/**'
            exclude 'mcmod.info'
            exclude 'plugin.yml'
            exclude 'bungee.yml'
        }
    }
}

minecraft {
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}