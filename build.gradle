import org.apache.tools.ant.filters.*

plugins {
    id 'java'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'com.palantir.git-version' version '0.12.0-rc2'
}

group 'com.github.creeper123123321.viafabric'
version gitVersion()
archivesBaseName = 'ViaFabric'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://repo.viaversion.com/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://maven.fabricmc.net/' }
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade('us.myles:viaversion:2.0.0-18w50a') {
        transitive = false
        changing = true
    }

    minecraft "com.mojang:minecraft:18w50a"
    mappings "net.fabricmc:yarn:18w50a.64"
    modCompile "net.fabricmc:fabric-loader:0.3.0.74"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:0.1.2.63"
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            //exclude 'META-INF', 'META-INF/**'
            exclude 'us/myles/ViaVersion/SpongePlugin.class'
            exclude 'us/viaversion/libs/javassist/**'
            exclude 'mcmod.info'
        }
    }
}

minecraft {
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}